import java_cup.runtime.*;
import java.util.ArrayList;

action code {:
    ArrayList<ArrayList<Double>> mat = new ArrayList();
    ArrayList<Double> row = new ArrayList();

    ArrayList<ArrayList<Double>> newRow(ArrayList<ArrayList<Double>> m) {
        m.add(row);
        row = new ArrayList<>();
        return m;
    }

    ArrayList<ArrayList<Double>> addRowValue(ArrayList<ArrayList<Double>> m, double n) {
        row.add(n);
        return m;
    }

    ArrayList<ArrayList<Double>> newMat() {
        return new ArrayList<>();
    }

    ArrayList<ArrayList<Double>> doOperation(int op, ArrayList<ArrayList<Double>> _m1, ArrayList<ArrayList<Double>> _m2) {
        double[][] result = null;
        try {
            double[][] m1 = Matrices.toArray(_m1);
            double[][] m2 = Matrices.toArray(_m2);

            switch (op) {
                case 2:
                    op -= 2;
                    m2 = Matrices.inversa(m2);
                case 0:
                    result = Matrices.producto(m1, m2); 
                    break;
                case 3:
                    op -= 2;
                    m2 = Matrices.producto(-1, m2);
                case 1:
                    result = Matrices.suma(m1, m2); 
                    break;
            }
        } catch (ArrayIndexOutOfBoundsException e) {
            printError(4+op);
        } finally {
            mat = newMat();
        }
        return result != null ? Matrices.toArrayList(result) : null;
    }

    ArrayList<ArrayList<Double>> doFunction(int op, ArrayList<ArrayList<Double>> _m) {
        double[][] result = null;
        try {
            double[][] m = Matrices.toArray(_m);

            switch (op) {
                case 0:
                    result = Matrices.transpuesta(m);
                    break;
                case 1:
                    if (m.length == m[m.length-1].length) {
                        result = Matrices.inversa(m);
                    } else {
                        printError(1+op);
                    }
                    break;
                case 2:
                    result = Matrices.adjunta(m);
                    break;
                default:
                    break;
            }

        } catch (ArrayIndexOutOfBoundsException e) {
            printError(1+op);
        }
        return result != null ? Matrices.toArrayList(result) : null;
    }

    ArrayList<ArrayList<Double>> checkMatrix(ArrayList<ArrayList<Double>> m) {

        mat = newMat();
        try {
            if (m.size() == 0) {
                printError(0);
                return mat;
            }
            return m;
        } catch (IndexOutOfBoundsException e) {
            printError(1);

        }

        return mat;
    }

    void printError(int err) { 
        String errMsg = "Syntax error";
        switch (err) {
            case 1:
                errMsg = Matrices.ERROR_FILAS;
                break;
            case 2:
                errMsg = Matrices.ERROR_INVERSA;
                break;
            case 3:
                errMsg = Matrices.ERROR_ADJUNTA;
                break;
            case 4:
                errMsg = Matrices.ERROR_PROD;
                break;
            case 5:
                errMsg = Matrices.ERROR_SUMA;
                break;
            default:
                break;
        }
        System.out.print(errMsg);
    }

    void print(double[][] mat) {
        if (mat != null) {
            Matrices.print(mat);
        }
    }

    void print(ArrayList<ArrayList<Double>> mat) {
        try {
            if (mat != null) {
                Matrices.print(mat);
            }
        } catch (Exception e) {
            printError(1);
        }
    }
:};

terminal ALL, CLL, AP, CP, AC, CC, COMA, PYC, ASIG, MAS, MENOS, POR, DIV, INVERSA, TRANSPUESTA, ADJUNTA, PRINT;
terminal String IDENT;
terminal Double NUMERO;
non terminal linea;
non terminal ArrayList<ArrayList<Double>> exp, mat, matll;
non terminal Integer func;

precedence left MAS, MENOS;
precedence left POR, DIV;
precedence left COMA, PYC;


linea ::= PRINT AP exp:res CP PYC   {: print(res); :} linea
        | PRINT AP exp:res CP PYC   {: print(res); :}
        | IDENT:id ASIG exp:res PYC {: TablaSimbolos.insertar(id, Matrices.toArray(res)); :} linea
        | IDENT:id ASIG exp:res PYC {: TablaSimbolos.insertar(id, Matrices.toArray(res)); :}
        ;

exp ::=   AC mat:m CC               {: RESULT = checkMatrix(newRow(m)); mat = newMat(); :}
        | ALL matll:m CLL           {: RESULT = checkMatrix(m); mat = newMat(); :}
        | func:f AP exp:m1 CP       {: RESULT = doFunction(f, m1); :}
        | exp:m1 POR exp:m2         {: RESULT = doOperation(0, m1, m2); :}
        | exp:m1 MAS exp:m2         {: RESULT = doOperation(1, m1, m2); :}
        | exp:m1 DIV exp:m2         {: RESULT = doOperation(2, m1, m2); :}
        | exp:m1 MENOS exp:m2       {: RESULT = doOperation(3, m1, m2); :}
        | IDENT:n                   {: RESULT = Matrices.toArrayList(TablaSimbolos.buscar(n)); :}
        | AP exp:m CP               {: RESULT = m; :}
        | AC CC                     {: System.out.print("Syntax error"); :}
        ;

matll ::= matll:e1 COMA             {: RESULT = e1; :} matll:e2 {: RESULT = e2; :}
        | ALL matll:e1 CLL          {: RESULT = newRow(e1); :}
        | NUMERO:n                  {: RESULT = addRowValue(mat, n); :}
        ;

mat ::=   mat:e1 COMA               {: RESULT = e1; :} mat:e2 {: RESULT = e2; :}
        | mat:e1 PYC                {: RESULT = newRow(e1); :} mat
        | NUMERO:n                  {: RESULT = addRowValue(mat, n); :}
        ;

func ::=  TRANSPUESTA               {: RESULT = 0; :}
        | INVERSA                   {: RESULT = 1; :}
        | ADJUNTA                   {: RESULT = 2; :}
        ;